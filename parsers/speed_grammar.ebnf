start: _speed_defs

_speed_defs: _speed_def
           | _speed_def "," _speed_defs

_speed_def: single_speed_def
          | multilane_speed

single_speed_def: _speed             -> normal_speed
                | "advisory:" _speed -> advisory_speed

multilane_speed: single_speed_def ("|" single_speed_def)+

_speed: speed_value _restriction_def
      | speed_value

_restriction_def: "(" _restrictions ")"

_restrictions: restriction
             | restriction "," _restrictions

restriction: weight                          -> weight_restriction
           | restriction_conditional         -> conditional_restriction
           | NUMBER+ LENGTH_UNIT             -> length_restriction
           | NUMBER+ "seats"                 -> seat_restriction
           | NUMBER+ "axles"                 -> axle_restriction
           | NUMBER+ "trailers"              -> trailers_restriction

restriction_conditional: "articulated"  -> articulated
                       | "trailer"      -> trailer
                       | "caravan"      -> caravan
                       | "wet"          -> wet
                       | time_span      -> time

weight: WEIGHT WEIGHT_UNIT                   -> weight_rating
      | WEIGHT_QUALIFIER WEIGHT WEIGHT_UNIT  -> qualified_weight_pre
      | WEIGHT WEIGHT_UNIT WEIGHT_QUALIFIER  -> qualified_weight_post

interval: TIME "-" TIME  -> neg_interval
        | TIME "+" TIME  -> pos_interval

time_span: interval                               -> interval
         | "(" interval ")" "-" "(" interval ")"  -> complex_time_span

speed_value: NUMBER+ "mph" -> mph_speed
           | NUMBER+       -> kph_speed
           | "walk"        -> walk_speed

WEIGHT: /\d+([.]\d+)?/
WEIGHT_QUALIFIER: "empty" | "capacity" | "trailer" | "current"
WEIGHT_UNIT: "t" | "st" | "lt" | "lb"

LENGTH_UNIT: "m" | "ft"

TIME: /[a-zA-Z0-9:]+/

%import common.INT -> NUMBER
%import common.WS
%ignore WS
